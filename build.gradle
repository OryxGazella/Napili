apply plugin: 'groovy'
apply plugin: 'idea'

defaultTasks 'clean', 'run'

version = appVersion
distsDirName = '../dist'


buildscript {

    //buildDir = 'build'

    final javafxHome = System.env['JAVAFX_HOME']
    if (javafxHome) {
      ext.javafxJar = "${javafxHome}/rt/lib/jfxrt.jar"
      ext.javafxAntJar = "${javafxHome}/lib/ant-javafx.jar"
    } else {
      def javaHome = System.env['JAVA_HOME']
      if (!javaHome) {
          javaHome = System.properties['java.home'].replaceFirst(/\/jre$/,'')
      }
      if (javaHome) {
        final javafxJar = "${javaHome}/jre/lib/jfxrt.jar"
        if ((new File(javafxJar)).exists())
            ext.javafxJar = javafxJar
            ext.javafxAntJar = "${javaHome}/lib/ant-javafx.jar"
      } 
    }
    try {
      println "JavaFX runtime jar: ${ext.javafxJar}"
      println "JavaFX antlib jar: ${ext.javafxAntJar}"
      dependencies {
        classpath files(ext.javafxJar)
        classpath files(ext.javafxAntJar)
      }
    }
    catch (MissingPropertyException mpe) {
      println """    Please set the environment variable JAVAFX_HOME
    to the directory that contains rt/lib/jfxrt.jar
    of JavaFX version ${requiredJavaFxVersion}."""
      System.exit 1
    }

}

repositories {
    mavenCentral()
}

dependencies {
  groovy "org.codehaus.groovy:groovy-all:${requiredGroovyVersion}"
  compile files(ext.javafxJar)
}

sourceSets {
    main {
        groovy {
            srcDir 'src'
        }
    }
}

task wrap(type: Wrapper, description: "create a gradlew") {
    gradleVersion = '1.1'
}

clean.doFirst {
    delete 'out'
    delete  distsDir
}


/*
task run(type: JavaExec, dependsOn: 'jar') {
    description = "Run the Napili Program"
    main = 'org.netdance.napili.Napili'
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
}
*/

task run(type: JavaExec, dependsOn: 'dist', description: 'Run the application, after building the jar') {
    main = '-jar'
    args = ["${distsDir}/Napili.jar"]
}

task libcopy(type: Sync) {
    from configurations.runtime
    into "${distsDir}/libs"
}

task dist(dependsOn: ['classes','libcopy']) << {

    ant.taskdef(resource: "com/sun/javafx/tools/ant/antlib.xml",
                uri: "javafx:com.sun.javafx.tools.ant",
                classpath: ".:${project.ext.javafxAntJar}" )

    def javafxAnt = groovy.xml.NamespaceBuilder.newInstance(ant, 'javafx:com.sun.javafx.tools.ant')

    javafxAnt.resources(id: 'libRes')   {
        javafxAnt.fileset(dir: distsDir, includes: 'libs/*.jar', type: 'jar', requiredFor: 'startup')
    }

    javafxAnt.application(id: "NapiliApp", name: "Napili Turtle Graphics", mainClass: appMainClass)

    javafxAnt.jar(destfile: "${distsDir}/Napili.jar") {
        javafxAnt.application(refid: 'NapiliApp')
        javafxAnt.resources(refid: 'libRes')
        fileset(dir: "${buildDir}/classes/main")
        fileset(dir: "${distsDir}/libs", includes: "*.jar")
    }


    javafxAnt.deploy(width: appWidth, height: appHeight, nativeBundles: 'all', outdir: distsDir, outfile: appTitle){
        javafxAnt.application(name: appTitle, mainClass: appMainClass)
        javafxAnt.resources {
            fileset(dir: buildDir, includes: '*.jar')
        }
        javafxAnt.info(title: appTitle, vendor: appVendor)
    }

}

idea {
    module {
        excludeDirs += file('gradle/') // Gradle directory including the wrapper subdirectory.
        excludeDirs += file('.settings/') // Eclipse settings directory.
        excludeDirs += file('bin') // Eclipse compilation directory.
        excludeDirs += file('out') // IDEA compilation directory.
        excludeDirs += file('build') // Gradle compilation directory.
        excludeDirs += file('dist') // Gradle/netbeans dist directory.
    }
    project {
        ipr {
            withXml { provider ->
                final node = provider.asNode()
                final component = provider.asNode().component
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }.mapping[0].'@vcs' = 'Git'
                final gradleSettings = node.appendNode('component' , [name: 'GradleSettings'])
                gradleSettings.appendNode('option', [name: 'linkedProjectPath', value: '$PROJECT_DIR$/build.gradle'])
            }
        }
    }
}

